# -----------------------------------------------------------------------
# [y] hybris Platform
#
# Copyright (c) 2017 SAP SE or an SAP affiliate company.  All rights reserved.
#
# This software is the confidential and proprietary information of SAP
# ("Confidential Information"). You shall not disclose such Confidential
# Information and shall use it only in accordance with the terms of the
# license agreement you entered into with SAP.
# -----------------------------------------------------------------------


*** Settings ***
Test Setup        import sampledata from resource "/configurablebundleservices/test/nestedBundleTemplates.impex"
Test Teardown     reset system

Resource          bundleservices/Add_To_Cart_Keywords.txt
Resource          bundleservices/Common_Keywords.txt
Resource          commerce/CommerceServices_Keywords.txt
Resource		  commerce/SubscriptionCommerce_Keywords.txt

*** Test Cases ***

### Add bundle to cart and verify price:

Test_Add_To_Cart_SingleBundleNew_SingleProduct
	[Documentation]    Adding a single product to a new bundle.
	verify that cart total is "0"
	add to cart product "PRODUCT01" for component "ProductComponent1" 	
    verify that number of child carts is "0" 
	verify that cart total for "paynow" is "600"
	verify that cart total is "600"	    
  
Test_Add_To_Cart_SingleBundleExisting_SingleProduct  
	[Documentation]    Adding a single product to a existing bundle.
	verify that cart total is "0"
	add to cart product "PRODUCT01" for component "ProductComponent1"
	verify that cart total is "600"
	add to cart product "PREMIUM01" for component "PremiumComponent2" to bundle with index "1"
	verify that cart total is "1310"

Test_Add_To_Cart_SingleBundleNew_TwoProducts
	[Documentation]    Adding a two products to a new bundle.
	add to cart product "PRODUCT01" for component "ProductComponent1" and product "PREMIUM01" for component "PremiumComponent2" 
	verify that cart total is "1310"
        
Test_Add_To_Cart_SingleBundleExisting_TwoProducts
	[Documentation]    Adding a two products to a new bundle.
	add to cart product "PRODUCT01" for component "ProductComponent1"
	verify that cart total is "600"
	add to cart product "PRODUCT02" for component "ProductComponent1" and product "PREMIUM02" for component "PremiumComponent2" to bundle with index "1"
	verify that cart total is "1970"

Test_AddAStandaloneProductAsABundled
    [Documentation]    Standalone product can not be added with a bundle
    Run keyword and expect error
    ...    Product 'STANDALONE01' is not in the products list of component (bundle template) 'ProductComponent1'
    ...    add to cart product "STANDALONE01" for component "ProductComponent1"

Test_AddAProductOfAnotherBundle
    [Documentation]    Product from another bundle can not be added with the bundle
    Run keyword and expect error
    ...    Product 'PRODUCT01' is not in the products list of component (bundle template) 'PremiumComponent2'
    ...    add to cart product "PRODUCT01" for component "PremiumComponent2"

Test_IndividualProductCanBeSoldSeparately
    [Documentation]    Products with soldIndividually=true can be sold out of bundle
    verify that cart total is "0"
    add product "PRODUCT05" to cart
    verify that cart total is "650"

Test_NotIndividualProductCanNotBeSoldSeparately
    [Documentation]    Not individual products can not be sold separately
    Run keyword and expect error
    ...    The given product 'PRODUCT06' must not be sold individually.
    ...    add product "PRODUCT06" to cart

Test_NotIndividualProductCanBeSoldInABundle
    [Documentation]    Not individual products can be sold as a part of bundle
    verify that cart total is "0"
    add to cart product "PRODUCT06" for component "ProductComponent1"
    verify that cart total is "600"

Test_Add_To_Cart_TheSameProduct_to_TheSameBundle
	[Documentation]    Adding the same product to the same bundle is prohibited.
	verify that cart total is "0"
	add to cart product "PRODUCT01" for component "ProductComponent1"
	verify that cart total is "600"
	Run keyword and expect error
    ...    Product 'PRODUCT01' is already in the cart for component (bundle template) 'ProductComponent1' and bundleNo 1
    ...    add to cart product "PRODUCT01" for component "ProductComponent1" to bundle with index "1"

Test_PickNtoMOverflow
    [Documentation]    'Pick N to M' criterion should limit maximum number of products being added to single bundle
    add to cart product "PRODUCT01" for component "OptionalComponent"
    Run keyword and expect error
    ...    Upper limit of possible product selections for component 'OptionalComponent' reached; items allowed: 1; items selected: 1
    ...    add to cart product "PRODUCT02" for component "OptionalComponent" to bundle with index "1"

Test_PickMOverflow
    [Documentation]    'Pick exactly N' criterion should limit maximum number of products being added to single bundle
    add to cart product "PRODUCT01" for component "ProductComponent1"
    add to cart product "PRODUCT02" for component "ProductComponent1" to bundle with index "1"
    add to cart product "PRODUCT03" for component "ProductComponent1" to bundle with index "1"
    Run keyword and expect error
    ...    Upper limit of possible product selections for component 'ProductComponent1' reached; items allowed: 3; items selected: 3
    ...    add to cart product "PRODUCT04" for component "ProductComponent1" to bundle with index "1"

Test_Add_To_Cart_NewBundle_CheckQuantity_Two
    [Documentation]    2 items of product cannot be added  to a new bundle component
    Run keyword and expect error
    ...    The given quantityToAdd (2) exceeds the max. allowed quantity (1)
    ...    add "2" items of product "PRODUCT01" for component "ProductComponent1" to bundle "-1"

Test_Add_To_Cart_ExistingBundle_CheckQuantity_Two
    [Documentation]    2 items of product cannot be added  to an existing bundle component
    add to cart product "PRODUCT01" for component "ProductComponent1"
    Run keyword and expect error
    ...    The given quantityToAdd (2) exceeds the max. allowed quantity (1)
    ...    add "2" items of product "PRODUCT02" for component "ProductComponent1" to bundle "1"

Test_Add_To_Cart_NewBundle_CheckQuantity_One
    [Documentation]    Only one piece of product can be added to a new  bundle
    add "1" items of product "PRODUCT01" for component "ProductComponent1" to bundle "-1"
    verify that cart total is "600"

Test_Add_To_Cart_ExistingBundle_CheckQuantity_One
    [Documentation]    Only one piece of product can be added to an existing bundle
    add "1" items of product "PRODUCT01" for component "ProductComponent1" to bundle "-1"
    add "1" items of product "PRODUCT02" for component "ProductComponent1" to bundle "1"
    verify that cart total is "1250"

Test_Add_To_Cart_NewBundle_AddQuantity_Zero
    [Documentation]    Product with quantity=0 cannot be added to a new bundle component
    Run keyword and expect error
    ...    Quantity must not be less than one
    ...    add "0" items of product "PRODUCT02" for component "ProductComponent1" to bundle "-1"

Test_Add_To_Cart_ExistingBundle_AddQuantity_Zero
    [Documentation]    A product with quantity=0 cannot be added to an existing bundle component
    add to cart product "PRODUCT01" for component "ProductComponent1"
    Run keyword and expect error
        ...    Quantity must not be less than one
        ...    add "0" items of product "PRODUCT02" for component "ProductComponent1" to bundle "1"

Test_NewBundleIsGenerated
    [Documentation]    Check that giving bundle index of -1 generates new bundle.
    ...                The test is indirect: single bundle is created and filled with products
    ...                up to the limit, so adding more products is impossible.
    ...                Then one more product is added to bundle '-1'. Successfull result means new bundle was created
    add to cart product "PRODUCT01" for component "ProductComponent1"
    add to cart product "PRODUCT02" for component "ProductComponent1" to bundle with index "1"
    add to cart product "PRODUCT03" for component "ProductComponent1" to bundle with index "1"
    verify that cart total is "2100"
    Run keyword and expect error
    ...    Upper limit of possible product selections for component 'ProductComponent1' reached; items allowed: 3; items selected: 3
    ...    add to cart product "PRODUCT04" for component "ProductComponent1" to bundle with index "1"
    add to cart product "PRODUCT04" for component "ProductComponent1" to bundle with index "-1"
    verify that cart total is "3050"

Test_BundleIndex_MustBeGreaterThan_Minus2
    [Documentation]   Allowed values of bunde index are -1, 0, 1, ...
    Run keyword and expect error
    ...    IllegalArgumentException: The bundleNo must not be lower then '-1', given bundleNo: -2
    ...    add to cart product "PRODUCT01" for component "ProductComponent1" to bundle with index "-2"

Test_BundleIndexMinus1_means_NewBundle
    [Documentation]  Bundle index -1 means 'put product into a new bundle"
	verify that cart total is "0"
	add to cart product "PRODUCT01" for component "ProductComponent1" to bundle with index "-1"
	verify that cart total is "600"

Test_ComponentWithoutBundle_ShouldFail
    [Documentation]   Bundle index of 0 means 'out of bundle'. That case should not be accompanied with component id
    Run keyword and expect error
    ...    IllegalArgumentException: Either bundleNo and bundleTemplate must be provided or none of both.*
    ...    add to cart product "PRODUCT01" for component "ProductComponent1" to bundle with index "0"

Test_ExistingBundleIndex_ShoudlAddToCart
    [Documentation]   If bundle index is positive, product should be added to the given bundle
    # It will create bundle #1
    add to cart product "PRODUCT01" for component "ProductComponent1"
    add to cart product "PRODUCT02" for component "ProductComponent1" to bundle with index "1"
	verify that cart total is "1250"

Test_NonExistingBundleIndex_ShouldFail
    [Documentation]   If bundle index is positive, given bundle should exist
    Run keyword and expect error
    ...    Can't determine parentBundleTemplateModel
    ...    add to cart product "PRODUCT01" for component "ProductComponent1" to bundle with index "100"

Test_Add_To_Cart_TheSameProduct_to_DifferentBundles
	[Documentation]    Adding the same product to different bundles is allowed.
	verify that cart total is "0"
	add to cart product "SHARED01" for component "ProductComponent1"
	verify that cart total is "99"
	add to cart product "SHARED01" for component "PremiumComponent2" to bundle with index "1"
	verify that cart total is "198"

Test_ReplaceProductInBundle
    [Documentation]   Replace a bundle product in cart
    add to cart product "PRODUCT01" for component "ProductComponent1"
    verify that cart total is "600"
    replace product in bundle "1" with product "PRODUCT02" of component "ProductComponent1"
    verify that cart total is "650"

Test_ReplaceProductInBundle_IncorrectBundleNo
    [Documentation]   Try to replace product giving incorrect index of bundle
    add to cart product "PRODUCT01" for component "ProductComponent1"
    verify that cart total is "600"
    Run keyword and expect error
    ...    CommerceCartModificationException: Can't determine parentBundleTemplateModel
    ...    replace product in bundle "2" with product "PRODUCT02" of component "ProductComponent1"

Test_ReplaceProductInNewBundle
    [Documentation]    Replace in new bundle works as addition
    add to cart product "PRODUCT01" for component "ProductComponent1"
    replace product in bundle "-1" with product "PRODUCT02" of component "ProductComponent1"
    verify that cart total is "1250"

Test_ReplaceProductInBundle_NegativeBundleNo
    [Documentation]   Try to replace product giving negative index of bundle
    add to cart product "PRODUCT01" for component "ProductComponent1"
    verify that cart total is "600"
    Run keyword and expect error
    ...    IllegalArgumentException: The bundleNo must not be lower then '-1', given bundleNo: -2
    ...    replace product in bundle "-2" with product "PRODUCT02" of component "ProductComponent1"

Test_ReplaceProductInBundle_IncorrectComponentId
    [Documentation]   Try to replace product giving incorrect component id
    add to cart product "PRODUCT01" for component "ProductComponent1"
    verify that cart total is "600"
    Run keyword and expect error
    ...    CommerceCartModificationException: Product 'PRODUCT02' is not in the products list of component (bundle template) 'PremiumComponent2'
    ...    replace product in bundle "1" with product "PRODUCT02" of component "PremiumComponent2"

Test_ReplaceProductInBundle_NonExistingComponentId
    [Documentation]   Try to replace product giving unknown id of component
    add to cart product "PRODUCT01" for component "ProductComponent1"
    verify that cart total is "600"
    Run keyword and expect error
    ...    ModelNotFoundException: No result for the given query
    ...    replace product in bundle "1" with product "PRODUCT02" of component "PremiumComponent99"

Test_ChangeComponent_ComponentIdOmitted
    [Documentation]   Replace to another product and do not provide component id
    add to cart product "PRODUCT01" for component "ProductComponent1"
    verify that cart total is "600"
    Run keyword and expect error
    ...    IllegalArgumentException: Parameter bundleTemplate can not be null
    ...    replace product in bundle "1" with product "PRODUCT02" of component ""

Test_ChangeComponent_AnotherProduct
    [Documentation]   Replace existing bundle in cart with another component id and another product.
    add to cart product "PRODUCT05" for component "ProductComponent1"
    add to cart product "PREMIUM01" for component "PremiumComponent2"
    verify that cart total is "1360"
    replace product in bundle "1" with product "PRODUCT01" of component "ProductComponent1"
    verify that cart total is "1310"

Test_ChangeComponent_AnotherComponent
    [Documentation]   Try to change bundle product to another component.
    ...               The original product should remain, as it noes not belong to the component provided.
    add to cart product "PRODUCT05" for component "ProductComponent1"
    replace product in bundle "1" with product "PRODUCT01" of component "OptionalComponent"
    verify that cart total is "1250"

Test_ChangeComponent_AnotherBundle
    [Documentation]   Try to change bundle product to a component from another bundle.
    ...               The original product should remain, as it noes not belong to the component provided.
    add to cart product "PRODUCT05" for component "ProductComponent1"
    replace product in bundle "1" with product "PRODUCT01" of component "SecondComponent1"
    verify that cart total is "1250"

Test_Add_To_Cart_EmptyCart_NoRequiredComponent_ShouldFail
	[Documentation]    Adding dependent component to empty cart is not allowed.
	verify that cart total is "0"
	Run keyword and expect error
	...    Component 'DependentComponent' cannot be modified as its selection dependency to component 'RequiredComponent' is not fulfilled (no entries yet)*
	...    add to cart product "PRODUCT01" for component "DependentComponent"

Test_Add_To_Cart_ExistingBundle_NoRequiredComponent_ShouldFail
	[Documentation]    Adding dependent component without required component to existing bundle is not allowed.
	verify that cart total is "0"
	add to cart product "SHARED01" for component "AnotherComponent"
	verify that cart total is "99"
	Run keyword and expect error
	...    Component 'DependentComponent' cannot be modified as its selection dependency to component 'RequiredComponent' is not fulfilled (no entries yet)*
	...    add to cart product "PRODUCT01" for component "DependentComponent" to bundle with index "1"

Test_Add_To_Cart_DifferentBundles_ShouldFail
	[Documentation]    Adding required component to one bundle and dependent component to another is not allowed.
	verify that cart total is "0"
	add to cart product "PRODUCT01" for component "RequiredComponent"
	verify that cart total is "600"
	Run keyword and expect error
	...    Component 'DependentComponent' cannot be modified as its selection dependency to component 'RequiredComponent' is not fulfilled (no entries yet)*
	...    add to cart product "PRODUCT02" for component "DependentComponent"

Test_Add_To_Cart_EmptyCart_RequiredComponent
	[Documentation]    Adding required and dependent components to empty cart.
	verify that cart total is "0"
	add to cart product "PRODUCT01" for component "RequiredComponent"
	verify that cart total is "600"
	add to cart product "PRODUCT02" for component "DependentComponent" to bundle with index "1"
	verify that cart total is "1250"

Test_Add_To_Cart_ExistingBundle_RequiredComponent
	[Documentation]    Adding required and dependent components to an existing bundle in a cart.
	verify that cart total is "0"
    add to cart product "PRODUCT01" for component "AnotherComponent"
    verify that cart total is "600"
	add to cart product "PRODUCT01" for component "RequiredComponent"
	verify that cart total is "1200"
	add to cart product "PRODUCT02" for component "DependentComponent" to bundle with index "2"
	verify that cart total is "1850"

Test_Add_To_Cart_DisableRules_ConditionalAndTargetProductsFromOneComponent
	[Documentation]    Adding conditional and target products from one component is not allowed by a disable rule.
	verify that cart total is "0"
	add to cart product "PRODUCT03" for component "ThirdComponent1"
	verify that cart total is "850"
	Run keyword and expect error
    ...    Product PRODUCT01 cannot be added as disable rule 'ThirdPackage_DisableProduct01WhenProduct03' of component 'ThirdComponent1' is violated
    ...    add to cart product "PRODUCT01" for component "ThirdComponent1" to bundle with index "1"

Test_Add_To_Cart_DisableRules_ConditionalAndTargetProductsFromDifferentComponents
	[Documentation]    Adding conditional and target products from 2 different components is not allowed by a disable rule.
	verify that cart total is "0"
	add to cart product "PRODUCT04" for component "ThirdComponent2"
	verify that cart total is "950"
	Run keyword and expect error
    ...    Product PRODUCT01 cannot be added as disable rule 'ThirdPackage_DisableProduct01WhenProduct04' of component 'ThirdComponent1' is violated
    ...    add to cart product "PRODUCT01" for component "ThirdComponent1" to bundle with index "1"

Test_Add_To_Cart_DisableRules_TargetProduct_AnyConditionalProductsInAnotherComponent
	[Documentation]    Adding a target product to bundle component after any conditional products in another component of the same bundle is not allowed by a disable rule.
	verify that cart total is "0"
	add to cart product "PREMIUM01" for component "ThirdComponent3"
	verify that cart total is "710"
	Run keyword and expect error
    ...    Product PRODUCT01 cannot be added as disable rule 'ThirdPackage_DisableProduct01WithAnyPremiumProduct' of component 'ThirdComponent1' is violated
    ...    add to cart product "PRODUCT01" for component "ThirdComponent1" to bundle with index "1"

Test_Add_To_Cart_DisableRules_TargetProduct_AllConditionalProductsInAnotherComponent
	[Documentation]    Adding a target product to bundle component with all conditional products in another component of the same bundle is not allowed by a disable rule.
	verify that cart total is "0"
	add to cart product "PREMIUM01" for component "ThirdComponent3"
	add to cart product "PREMIUM02" for component "ThirdComponent3" to bundle with index "1"
	add to cart product "PREMIUM03" for component "ThirdComponent3" to bundle with index "1"
	add to cart product "PREMIUM04" for component "ThirdComponent3" to bundle with index "1"
	add to cart product "PREMIUM05" for component "ThirdComponent3" to bundle with index "1"
	add to cart product "PREMIUM06" for component "ThirdComponent3" to bundle with index "1"
	verify that cart total is "4410"
	Run keyword and expect error
    ...    Product PRODUCT02 cannot be added as disable rule 'ThirdPackage_DisableProduct02WithAllPremiumProducts' of component 'ThirdComponent1' is violated
    ...    add to cart product "PRODUCT02" for component "ThirdComponent1" to bundle with index "1"

Test_Add_To_Cart_NoDisableRules
	[Documentation]    Adding products without disable rules is allowed.
	verify that cart total is "0"
	add to cart product "PRODUCT03" for component "ThirdComponent1"
	verify that cart total is "850"
	add to cart product "PRODUCT02" for component "ThirdComponent1" to bundle with index "1"
	verify that cart total is "1500"


Test_Add_To_Cart_DisableRules_ChangePriceRuleForTheSameProducts
	[Documentation]    Adding products with configured ChangePriceRule and DisableRule is not allowed by by the disable rule.
	verify that cart total is "0"
	add to cart product "PRODUCT01" for component "ThirdComponent1"
	verify that cart total is "600"
	Run keyword and expect error
    ...    Product SHARED01 cannot be added as disable rule 'ThirdPackage_DisableSHARED01WhenProduct01' of component 'ThirdComponent5' is violated
    ...    add to cart product "SHARED01" for component "ThirdComponent5" to bundle with index "1"

Test_Add_To_Cart_DisableRules_NoConditionalProducts
	[Documentation]    Adding target product if conditional products weren't configured for disable rule is allowed.
	verify that cart total is "0"
	add to cart product "PRODUCT01" for component "ThirdComponent1"
	verify that cart total is "600"
	add to cart product "SHARED02" for component "ThirdComponent6" to bundle with index "1"
	verify that cart total is "699"

Test_Add_To_Cart_DisableRules_NoTargetProducts
	[Documentation]    Adding bundle if target products weren't configured for disable rule is allowed.
	verify that cart total is "0"
	add to cart product "SHARED01" for component "ThirdComponent5"
	verify that cart total is "99"
	add to cart product "SHARED02" for component "ThirdComponent7" to bundle with index "1"
	verify that cart total is "198"

Test_Add_To_Cart_DisableRules_ConditionalAndTargetProductsFromDifferentBundles
	[Documentation]    Adding the conditional and target products to different bundles is allowed.
	verify that cart total is "0"
	add to cart product "PRODUCT03" for component "ThirdComponent1"
	verify that cart total is "850"
	add to cart product "PRODUCT01" for component "ThirdComponent1"
	verify that cart total is "1450"

Test_Add_To_Cart_DisableRules_ConditionalProductIsBundled_TargetIsStandalone
	[Documentation]    Adding a conditional product to a bundle and a target product as a standalone product is allowed.
	verify that cart total is "0"
	add to cart product "PRODUCT03" for component "ThirdComponent1"
	verify that cart total is "850"
	add product "PRODUCT01" to cart
	verify that cart total is "1450"

Test_Add_To_Cart_DisableRules_ConditionalProductIsStandalone_TargetProductIsInBundled
	[Documentation]    Adding a conditional product as a standalone product and a target product to a bundle is allowed.
	verify that cart total is "0"
	add product "PRODUCT03" to cart
	verify that cart total is "850"
	add to cart product "PRODUCT01" for component "ThirdComponent1"
	verify that cart total is "1450"

Test_Add_To_Cart_DisableRules_TargetIsAlreadyInCart
	[Documentation]    Adding a conditional product to the bundle after a target product is not allowed by disable rule.
	verify that cart total is "0"
	add to cart product "PRODUCT01" for component "ThirdComponent1"
	verify that cart total is "600"
	Run keyword and expect error
	...   Product PRODUCT03 cannot be added as disable rule 'ThirdPackage_DisableProduct01WhenProduct03' of component 'ThirdComponent1' is violated
	...    add to cart product "PRODUCT03" for component "ThirdComponent1" to bundle with index "1"
